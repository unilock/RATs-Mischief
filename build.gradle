plugins {
	id 'fabric-loom' version '1.9.+'
}

version = "${project.mod_version}+${project.minecraft_version}+fabric"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

loom {
	mods {
		'ratsmischief' {
			sourceSet sourceSets.main
		}
	}
}

repositories {
	// GeckoLib
	maven {
		name = 'GeckoLib'
		url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
	}

	// CCA
	maven {
		name = 'Ladysnake Mods'
		url = 'https://maven.ladysnake.org/releases/'
	}

	// Trinkets, Mod Menu
	maven {
		name = 'TerraformersMC'
		url = 'https://maven.terraformersmc.com/'
	}

	// Step Height Entity Attribute
	maven {
		name = 'JitPack'
		url = 'https://jitpack.io/'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}+${project.minecraft_version}"

	// GeckoLib
	modImplementation("software.bernie.geckolib:${project.geckolib_version}") {
		exclude group: 'net.fabricmc'
		exclude group: 'net.fabricmc.fabric-api'
	}

	// SHEA
	modImplementation include("com.github.emilyploszaj:step-height-entity-attribute:${project.shea_version}") {
		exclude group: 'net.fabricmc'
		exclude group: 'net.fabricmc.fabric-api'
	}

	// Trinkets
	modImplementation("dev.emi:trinkets:${project.trinkets_version}") {
		exclude group: 'net.fabricmc'
		exclude group: 'net.fabricmc.fabric-api'
	}

	// Mod Menu
	modLocalRuntime("com.terraformersmc:modmenu:${mod_menu_version}") {
		transitive = false
	}
}

processResources {
	inputs.property 'version', version

	filesMatching('fabric.mod.json') {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from('LICENSE') {
		rename { "${it}_${base.archivesName.get()}" }
	}
}
